program ::= command*

command ::= axiom | check | definition | notation

axiom ::= "Axiom" identifier+ ":" expr "."

check ::= "Check" expr "."

definition ::= definition-keyword identifier param-group* ":" expr ":=" expr "."

definition-keyword ::= "Definition" | "Procedure"

param-group ::= "(" identifier+ type-ann? ")"
              | "{" identifier+ type-ann? "}"

simple-param-group ::= identifier+ type-ann?

type-ann ::= ":" expr

notation ::= "Notation" assoc prec "(" identifier ")" ":=" expr "."

assoc ::= "left" | "right" | "noassoc"

prec ::= nat

expr ::= fun
       | pi
       | ann

ann ::= term (":" expr)?

pi ::= pi-keyword param-group "," expr

pi-keyword ::= "âˆ€" | "Î " | "forall"

fun ::= "fun" param-group+ "=>" expr
      | "fun" simple-param-group "=>" expr

term ::= app (binary-tail)*

binary-tail ::= op app
              | "->" app

app ::= atom arg*

arg ::= atom
      | "_"
      | "{" expr "}"
      | "{" identifier "=" expr "}"

atom ::= univ
       | var
       | lit
       | hole
       | paren

univ ::= "*" | "ğ’°"

var ::= identifier

lit ::= nat | string

hole ::= "??"

paren ::= "(" expr ")"

op ::= identifier
